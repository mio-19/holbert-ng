let newline = "\n"
let mapMapValues = (m: Map.t<'a, 'b>, f: 'b => 'c) => {
  let nu = Map.make()
  m->Map.forEachWithKey((v, k) => {
    nu->Map.set(k, f(v))
  })
  nu
}

@send external toString: 'a => string = "toString"
let showArray: array<'a> => string = a => `[${Array.toString(a)}]`
let showTuple: (('a, 'b)) => string = ((a, b)) => `(${toString(a)} ${toString(b)})`

let prettyPrintMap = (
  m: Map.t<'k, 'v>,
  ~showK: 'k => string=toString,
  ~showV: 'v => string=toString,
) => {
  m
  ->Map.entries
  ->Iterator.toArray
  ->Array.map(((k, v)) => {
    (showK(k), showV(v))
  })
  ->showArray
}

let prettyPrintIntMap = (m: Belt.Map.Int.t<'v>, ~showV: 'v => string=toString) => {
  m
  ->Belt.Map.Int.toArray
  ->Array.map(((k, v)) => {
    (Int.toString(k), showV(v))
  })
  ->showArray
}

let prettyPrintVar = (idx: int, scope: array<string>) =>
  "$" ++
  switch scope[idx] {
  | Some(n) if Array.indexOf(scope, n) == idx => n
  | _ => "\\"->String.concat(String.make(idx))
  }
let prettyPrintSchematic = (schematic: int, allowed: array<int>, scope: array<string>) => {
  let allowedStr =
    allowed
    ->Array.map(idx => prettyPrintVar(idx, scope))
    ->Array.join(" ")
  `?${Int.toString(schematic)}(${allowedStr})`
}

let mapIntersectionWith = (m1: Map.t<'k, 'a>, m2: Map.t<'k, 'b>, f: ('a, 'b) => 'c) => {
  let go = (m1, m2) => {
    let nu: Map.t<'k, 'c> = Map.make()
    m1->Map.forEachWithKey((v1, k) => {
      switch m2->Map.get(k) {
      | Some(v2) => nu->Map.set(k, f(v1, v2))
      | None => ()
      }
    })
    nu
  }
  if Map.size(m1) < Map.size(m2) {
    go(m1, m2)
  } else {
    go(m2, m1)
  }
}

let mapUnionWith = (m1: Map.t<'k, 'a>, m2: Map.t<'k, 'a>, f: ('a, 'a) => 'a) => {
  let nu = Map.make()
  m1->Map.forEachWithKey((v1, k) => {
    switch m2->Map.get(k) {
    | Some(v2) => nu->Map.set(k, f(v1, v2))
    | None => nu->Map.set(k, v1)
    }
  })
  m2->Map.forEachWithKey((v, k) => {
    switch nu->Map.get(k) {
    | Some(_) => ()
    | None => nu->Map.set(k, v)
    }
  })
  nu
}

// left biased
let mapUnion = (m1, m2) => mapUnionWith(m1, m2, (v1, _v2) => v1)

let mapIntersection = (m1: Map.t<'k, 'a>, m2: Map.t<'k, 'b>): Map.t<'k, ('a, 'b)> =>
  mapIntersectionWith(m1, m2, (b, c) => (b, c))

let withKey: ('props, int) => 'props = %raw(`(props, key) => ({...props, key})`)

let mapEqual = (m1, m2) => {
  Map.size(m1) == Map.size(m1) &&
    mapIntersection(m1, m2)
    ->Map.values
    ->Iterator.toArray
    ->Array.filter(((a, b)) => a == b)
    ->Array.length == Map.size(m2)
}

module Map = {
  type t<'k, 'v> = Map.t<'k, 'v>
  let filterMap = (m: t<'k, 'v1>, f: ('k, 'v1) => option<'v2>): t<'k, 'v2> =>
    m
    ->Map.entries
    ->Iterator.toArrayWithMapper(((i, v)) => f(i, v)->Option.map(v => (i, v)))
    ->Array.keepSome
    ->Map.fromArray
  let update = (m: t<'k, 'v>, k: 'k, f: 'v => 'v, ~default: 'v) => {
    m->Map.set(k, Map.get(m, k)->Option.map(f)->Option.getOr(default))
  }
  let clone = (m: t<'k, 'v>) => m->Map.entries->Map.fromIterator
}

let arrayWithIndex = (arr: array<React.element>) => {
  React.array(arr->Array.mapWithIndex((m, i) => <span key={String.make(i)}> m </span>))
}
let execRe = (re, str) => {
  re
  ->RegExp.exec(str)
  ->Option.map(result => {
    open RegExp.Result
    (matches(result), fullMatch(result)->String.length)
  })
}

let identRegexStr = `([a-zA-Z][a-zA-Z\\d]*)`

let intersperse = (a: array<'a>, ~with: 'a) =>
  a->Array.flatMapWithIndex((e, i) =>
    if i == 0 {
      [e]
    } else {
      [with, e]
    }
  )

let updateAtIndex = (arr: array<'a>, targetIdx: int, newValue: 'a): array<'a> => {
  arr->Array.toSpliced(~start=targetIdx, ~remove=1, ~insert=[newValue])
}

exception Unreachable(string)
exception Err(string)
let mustFindIndex = (arr, f) => {
  switch Array.findIndex(arr, f) {
  | -1 => throw(Unreachable("Element not found"))
  | i => i
  }
}

module Result = {
  include Result
  type t<'a, 'b> = result<'a, 'b>
  let ok = (r: t<'a, 'b>): option<'a> =>
    switch r {
    | Ok(a) => Some(a)
    | Error(_) => None
    }
  let or = (r1: t<'a, 'b>, r2: unit => t<'a, 'b>): t<'a, 'b> =>
    switch r1 {
    | Ok(_) => r1
    | Error(_) => r2()
    }
}

module Option = {
  include Option
  let getOrElse = (t, f): 'a =>
    switch t {
    | Some(a) => a
    | None => f()
    }
  let sequence = (xs: array<option<'a>>): option<array<'a>> => {
    let filtered = xs->Array.keepSome
    if Array.length(xs) == Array.length(filtered) {
      Some(filtered)
    } else {
      None
    }
  }
}

module Hash = {
  // https://stackoverflow.com/questions/7616461/generate-a-hash-from-string-in-javascript
  let cyrb53: (string, ~seed: int=?) => int = %raw(`(str, seed = 0) => {
      let h1 = 0xdeadbeef ^ seed, h2 = 0x41c6ce57 ^ seed;
      for(let i = 0, ch; i < str.length; i++) {
          ch = str.charCodeAt(i);
          h1 = Math.imul(h1 ^ ch, 2654435761);
          h2 = Math.imul(h2 ^ ch, 1597334677);
      }
      h1  = Math.imul(h1 ^ (h1 >>> 16), 2246822507);
      h1 ^= Math.imul(h2 ^ (h2 >>> 13), 3266489909);
      h2  = Math.imul(h2 ^ (h2 >>> 16), 2246822507);
      h2 ^= Math.imul(h1 ^ (h1 >>> 13), 3266489909);

      return 4294967296 * (2097151 & h2) + (h1 >>> 0);
  }`)
}
