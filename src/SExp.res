exception SubstNotCompatible(string)

module type ATOM = AtomDef.ATOM

module IntCmp = Belt.Id.MakeComparable({
  type t = int
  let cmp = Pervasives.compare
})

module Make = (Atom: AtomDef.COERCIBLE_ATOM): {
  type rec t =
    | Atom(Atom.t)
    | Compound({subexps: array<t>})
    | Var({idx: int})
    | Schematic({schematic: int, allowed: array<int>})

  include Signatures.TERM
    with type t := t
    and type meta = string
    and type schematic = int
    and type subst = Map.t<int, t>
  let mapTerms: (t, t => t) => t
} => {
  type rec t =
    | Atom(Atom.t)
    | Compound({subexps: array<t>})
    | Var({idx: int})
    | Schematic({schematic: int, allowed: array<int>})
  module Atom = Atom
  type meta = string
  type schematic = int
  type subst = Map.t<schematic, t>
  let substEqual = Util.mapEqual
  let mapSubst = Util.mapMapValues
  let makeSubst = () => {
    Map.make()
  }
  let equivalent = (a: t, b: t) => {
    a == b
  }
  let reduce = (term: t) => term
  let rec schematicsIn: t => Belt.Set.t<int, IntCmp.identity> = (it: t) =>
    switch it {
    | Schematic({schematic, _}) => Belt.Set.make(~id=module(IntCmp))->Belt.Set.add(schematic)
    | Compound({subexps}) =>
      subexps->Array.reduce(Belt.Set.make(~id=module(IntCmp)), (s, x) =>
        Belt.Set.union(s, schematicsIn(x))
      )
    | _ => Belt.Set.make(~id=module(IntCmp))
    }
  let rec freeVarsIn: t => Belt.Set.t<int, IntCmp.identity> = (it: t) =>
    switch it {
    | Var({idx}) => Belt.Set.make(~id=module(IntCmp))->Belt.Set.add(idx)
    | Compound({subexps}) =>
      subexps->Array.reduce(Belt.Set.make(~id=module(IntCmp)), (s, x) =>
        Belt.Set.union(s, freeVarsIn(x))
      )
    | _ => Belt.Set.make(~id=module(IntCmp))
    }
  let rec substitute = (term: t, subst: subst) =>
    switch term {
    | Compound({subexps}) => Compound({subexps: Array.map(subexps, x => substitute(x, subst))})
    | Schematic({schematic, _}) =>
      switch Map.get(subst, schematic) {
      | None => term
      | Some(found) => found
      }
    | Atom(name) => {
        let symbolSubs =
          subst
          ->Map.entries
          ->Iterator.toArray
          ->Array.filterMap(((name, v)) =>
            switch v {
            | Atom(v) => Some((name, v))
            | _ => None
            }
          )
          ->Map.fromArray
        Atom(name->Atom.substitute(symbolSubs))
      }
    | _ => term
    }

  let combineSubst = (s: subst, t: subst) => {
    let nu = Map.make()
    Map.entries(s)->Iterator.forEach(opt =>
      switch opt {
      | None => ()
      | Some((key, term)) => nu->Map.set(key, term->substitute(t))
      }
    )
    Map.entries(t)->Iterator.forEach(opt =>
      switch opt {
      | None => ()
      | Some((key, term)) => nu->Map.set(key, term->substitute(s))
      }
    )
    nu
  }
  let emptySubst: subst = Map.make()
  let singletonSubst: (int, t) => subst = (schematic, term) => {
    let s = Map.make()
    s->Map.set(schematic, term)
    s
  }
  let rec unifyTerm = (a: t, b: t): Seq.t<subst> =>
    switch (a, b) {
    | (Atom(na), Atom(nb)) =>
      Atom.unify(na, nb)->Seq.map(subst => subst->Util.mapMapValues(v => Atom(v)))
    | (Compound({subexps: xa}), Compound({subexps: xb})) if Array.length(xa) == Array.length(xb) =>
      unifyArray(Belt.Array.zip(xa, xb))
    | (Var({idx: ia}), Var({idx: ib})) if ia == ib => Seq.once(emptySubst)
    | (Schematic({schematic: sa, _}), Schematic({schematic: sb, _})) if sa == sb =>
      Seq.once(emptySubst)
    | (Schematic({schematic, allowed}), t)
      if !Belt.Set.has(schematicsIn(t), schematic) &&
      Belt.Set.subset(freeVarsIn(t), Belt.Set.fromArray(allowed, ~id=module(IntCmp))) =>
      Seq.once(singletonSubst(schematic, t))
    | (t, Schematic({schematic, allowed}))
      if !Belt.Set.has(schematicsIn(t), schematic) &&
      Belt.Set.subset(freeVarsIn(t), Belt.Set.fromArray(allowed, ~id=module(IntCmp))) =>
      Seq.once(singletonSubst(schematic, t))
    | (_, _) => Seq.empty
    }
  and unifyArray = (a: array<(t, t)>): Seq.t<subst> => {
    if Array.length(a) == 0 {
      Seq.once(emptySubst)
    } else {
      let (x, y) = a[0]->Option.getUnsafe
      unifyTerm(x, y)->Seq.flatMap(s1 =>
        a
        ->Array.sliceToEnd(~start=1)
        ->Array.filterMap(((t1, t2)) =>
          try {Some((substitute(t1, s1), substitute(t2, s1)))} catch {
          | SubstNotCompatible(_) => None
          }
        )
        ->unifyArray
        ->Seq.filterMap(s2 =>
          try {Some(combineSubst(s1, s2))} catch {
          | SubstNotCompatible(_) => None
          }
        )
      )
    }
  }
  let unify = (a: t, b: t, ~gen as _=?) => unifyTerm(a, b)

  let rec lower = (term: t): option<Atom.t> =>
    switch term {
    | Atom(s) => Some(s)
    | Var({idx}) => Atom.coerce(HValue(AtomDef.SExpTag, AtomDef.Var({idx: idx})))
    | Schematic({schematic, allowed}) =>
      Atom.coerce(HValue(AtomDef.SExpTag, AtomDef.Schematic({schematic, allowed})))
    | Compound({subexps: [e1]}) => lower(e1)
    | _ => None
    }
  let rec substDeBruijn = (term: t, substs: array<t>, ~from: int=0) =>
    switch term {
    | Atom(s) => Atom(Atom.substDeBruijn(s, Array.map(substs, lower), ~from))

    | Compound({subexps}) =>
      Compound({subexps: Array.map(subexps, x => substDeBruijn(x, substs, ~from))})
    | Var({idx: var}) =>
      if var < from {
        term
      } else if var - from < Array.length(substs) && var - from >= 0 {
        Option.getUnsafe(substs[var - from])
      } else {
        Var({idx: var - Array.length(substs)})
      }
    | Schematic({schematic, allowed}) =>
      Schematic({
        schematic,
        allowed: Array.filterMap(allowed, i =>
          if i < from + Array.length(substs) {
            None
          } else {
            Some(i - (from + Array.length(substs)))
          }
        ),
      })
    }
  let rec upshift = (term: t, amount: int, ~from: int=0) =>
    switch term {
    | Atom(s) => Atom(s->Atom.upshift(amount, ~from))
    | Compound({subexps}) => Compound({subexps: Array.map(subexps, x => upshift(x, amount, ~from))})
    | Var({idx}) =>
      Var({
        idx: if idx >= from {
          idx + amount
        } else {
          idx
        },
      })
    | Schematic({schematic, allowed}) =>
      Schematic({
        schematic,
        allowed: Array.map(allowed, i =>
          if i >= from {
            i + amount
          } else {
            i
          }
        ),
      })
    }
  let place = (x: int, ~scope: array<string>) => Schematic({
    schematic: x,
    allowed: Array.fromInitializer(~length=Array.length(scope), i => i),
  })
  let mergeSubsts = Util.mapUnion

  type gen = ref<int>
  let seen = (g: gen, s: int) => {
    if s >= g.contents {
      g := s + 1
    }
  }
  let fresh = (g: gen, ~replacing as _=?) => {
    let v = g.contents
    g := g.contents + 1
    v
  }
  let prettyPrintVar = (idx: int, scope: array<string>) => {
    switch scope[idx] {
    | Some(n) if Array.indexOf(scope, n) == idx => n
    | _ => "\\"->String.concat(String.make(idx))
    }
  }
  let makeGen = () => {
    ref(0)
  }
  let rec prettyPrint = (it: t, ~scope: array<string>) =>
    switch it {
    | Atom(name) => Atom.prettyPrint(name, ~scope)
    | Var({idx}) => prettyPrintVar(idx, scope)
    | Schematic({schematic, allowed}) =>
      "?"
      ->String.concat(String.make(schematic))
      ->String.concat("(")
      ->String.concat(Array.join(allowed->Array.map(idx => prettyPrintVar(idx, scope)), " "))
      ->String.concat(")")
    | Compound({subexps}) =>
      "("
      ->String.concat(Array.join(subexps->Array.map(e => prettyPrint(e, ~scope)), " "))
      ->String.concat(")")
    }

  let prettyPrintSubst = (sub, ~scope) =>
    Util.prettyPrintMap(sub, ~showV=t => prettyPrint(t, ~scope))
  let nameRES = "^([^\\s.\\[\\]()]+)\\."
  let prettyPrintMeta = (str: string) => {
    String.concat(str, ".")
  }
  let parseMeta = (str: string) => {
    let re = RegExp.fromStringWithFlags(nameRES, ~flags="y")
    switch re->RegExp.exec(str->String.trim) {
    | None => Error("not a meta name")
    | Some(res) =>
      switch RegExp.Result.matches(res) {
      | [n] => Ok(n, String.sliceToEnd(str->String.trim, ~start=RegExp.lastIndex(re)))
      | _ => Error("impossible happened")
      }
    }
  }
  let mkParser = (~scope: array<string>, ~gen=?): Parser.t<t> => {
    open Parser
    let varLit = string("\\")->then(decimal)
    let ident = regex1(/([^\s\(\)]+)/)
    let varIdx =
      varLit
      ->or(
        ident->bind(id =>
          switch scope->Array.indexOfOpt(id) {
          | Some(idx) => pure(idx)
          | None => fail("expected variable")
          }
        ),
      )
      ->lexeme
    let var = varIdx->map(idx => Var({idx: idx}))

    let schemaLit =
      string("?")
      ->then(decimal)
      ->bind(schematic =>
        many(varIdx)
        ->between(token("("), token(")"))
        ->map(allowed => {
          gen->Option.map(g => allowed->Array.forEach(n => seen(g, n)))->ignore
          Schematic({schematic, allowed})
        })
      )
    let inner = fix(f =>
      choice([
        schemaLit,
        var,
        liftParse(Atom.parse, ~scope, ~gen?)->map(a => Atom(a)),
        many(f)
        ->between(token("("), token(")"))
        ->map(subexps => Compound({subexps: subexps})),
      ])->lexeme
    )
    whitespace->then(inner)
  }

  let parse = (str: string, ~scope: array<string>, ~gen=?) => {
    Parser.runParser(mkParser(~scope, ~gen?), str)->Result.mapError(e => e.message)
  }

  let rec concrete = t =>
    switch t {
    | Schematic(_) => true
    | Atom(s) => Atom.concrete(s)
    | Compound({subexps}) => subexps->Array.every(concrete)
    | _ => false
    }
  let mapTerms = (t, f) => f(t)
}
