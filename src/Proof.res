open Method
open Signatures

module Make = (
  Term: TERM,
  Judgment: JUDGMENT with module Term := Term,
  Method: PROOF_METHOD with module Term := Term and module Judgment := Judgment,
) => {
  module Rule = Rule.Make(Term, Judgment)
  module Context = Context(Term, Judgment)
  module Results = MethodResults(Term)

  type display = Full | Tree | Summary

  type rec t = {
    fixes: array<Term.meta>,
    assumptions: array<string>,
    method: option<Method.t<t>>,
    display: display,
  }

  type rec checked =
    | Checked({
        fixes: array<Term.meta>,
        assumptions: array<string>,
        method: checked_option_method,
        rule: Rule.t,
        display: display,
      })
    | ProofError({raw: t, rule: Rule.t, msg: string})
  and checked_option_method =
    | Do(Method.t<checked>)
    | Goal
  let parseKeyword = input => {
    Method.keywords
    ->Array.concat(["?"])
    ->Array.find(kw => String.trim(input)->String.startsWith(kw))
  }
  let rec substitute = (prf: t, subst: Term.subst) => {
    fixes: prf.fixes,
    assumptions: prf.assumptions,
    display: prf.display,
    method: prf.method->Option.map(m =>
      m->Method.substitute(subst)->Method.map(m => m->substitute(subst))
    ),
  }
  let rec prettyPrint = (prf: t, ~grammar, ~scope, ~assms, ~indentation=0) => {
    let mtd = switch prf.method {
    | None => "?"
    | Some(m) =>
      Method.prettyPrint(
        m,
        ~grammar,
        ~scope=prf.fixes->Array.concat(scope),
        ~assms=assms->Array.concat(prf.assumptions),
        ~indentation=indentation + 2,
        ~subprinter=prettyPrint,
      )
    }
    let turnstile = switch prf.display {
    | Full => "|-"
    | Tree => "|:"
    | Summary => "|."
    }
    let displayFixes = [...prf.fixes]
    Array.reverse(displayFixes)
    String.padStart("", indentation, " ")
    ->String.concat(
      displayFixes->Array.map(t => t->Term.prettyPrintMeta->String.concat(" "))->Array.join(""),
    )
    ->String.concat(prf.assumptions->Array.join(" "))
    ->String.concat(
      if Array.length(prf.assumptions) == 0 {
        `${turnstile} `
      } else {
        ` ${turnstile} `
      },
    )
    ->String.concat(mtd)
  }
  let rec parse = (input, ~grammar, ~scope, ~assms, ~gen) => {
    let it = ref(Error(""))
    let cur = ref(String.trim(input))
    let fixes = []
    while {
      it := Term.parseMeta(cur.contents)
      it.contents->Result.isOk
    } {
      let (n, r) = Result.getExn(it.contents)
      cur := String.trim(r)
      fixes->Array.unshift(n)
    }
    let it = ref(Error(""))
    let assumptions = []
    while {
      it := Rule.parseRuleName(cur.contents)
      it.contents->Result.isOk
    } {
      let (a, r) = Result.getExn(it.contents)
      cur := String.trim(r)
      assumptions->Array.push(a)
    }
    let turnstile = cur.contents->String.slice(~start=0, ~end=2)
    if turnstile != "|-" && turnstile != "|:" && turnstile != "|." {
      Console.log((fixes, assumptions))
      Error("expected turnstile or rule name"->String.concat(cur.contents))
    } else {
      let display = switch turnstile {
      | "|:" => Tree
      | "|." => Summary
      | _ => Full
      }
      cur := cur.contents->String.trim->String.sliceToEnd(~start=2)->String.trim
      let scope' = Array.concat(fixes, scope)
      let assms' = Array.concat(assms, assumptions)
      switch parseKeyword(cur.contents) {
      | Some("?") =>
        Ok(({fixes, assumptions, method: None, display}, cur.contents->String.sliceToEnd(~start=1)))
      | Some(keyword) => {
          cur := cur.contents->String.sliceToEnd(~start=String.length(keyword))
          switch Method.parse(
            cur.contents,
            ~grammar,
            ~keyword,
            ~scope=scope',
            ~assms=assms',
            ~gen,
            ~subparser=parse,
          ) {
          | Ok((method, r)) => Ok(({fixes, assumptions, method: Some(method), display}, r))
          | Error(e) => Error(e)
          }
        }
      | None => Error("Expected keyword")
      }
    }
  }
  let enter = (ctx: Context.t, prf: t, rule: Rule.t) => {
    let (nFixes, nVars) = (Array.length(prf.fixes), Array.length(rule.vars))
    if Array.length(prf.fixes) == Array.length(rule.vars) {
      let (nAssumptions, nPremises) = (Array.length(prf.assumptions), Array.length(rule.premises))

      if nAssumptions == nPremises {
        Ok({
          Context.fixes: prf.fixes->Array.concat(ctx.fixes),
          localFacts: ctx.localFacts
          ->Array.map(r => Rule.upshift(r, rule.vars->Array.length))
          ->Array.concat(rule.premises),
          localFactNames: ctx.localFactNames->Array.concat(prf.assumptions),
          globalFacts: ctx.globalFacts,
        })
      } else {
        Error(
          `Proof introduces a different number (${Int.toString(
              nAssumptions,
            )}) of assumptions than the rule (${Int.toString(nPremises)})`,
        )
      }
    } else {
      Error(
        `Proof introduces a different number (${Int.toString(
            nFixes,
          )}) of variables than the rule (${Int.toString(nVars)})`,
      )
    }
  } //result<Context, string>

  let toGoal = (prf: checked) =>
    switch prf {
    | ProofError(_) => prf
    | Checked({fixes, assumptions, method: _, rule, display}) =>
      Checked({fixes, assumptions, method: Goal, rule, display})
    }
  let rec uncheck = (prf: checked) =>
    switch prf {
    | ProofError({raw, rule: _, msg: _}) => raw
    | Checked({fixes, assumptions, method, rule: _, display}) => {
        fixes,
        assumptions,
        display,
        method: switch method {
        | Do(m) => Some(m->Method.map(uncheck))
        | Goal => None
        },
      }
    }
  let rec check = (ctx: Context.t, prf: t, rule: Rule.t) => {
    switch enter(ctx, prf, rule) {
    | Ok(ctx') =>
      switch prf.method {
      | Some(m) =>
        switch m->Method.check(ctx', rule.conclusion, (s, r) => check(ctx', s, r)) {
        | Ok(m') =>
          Checked({
            rule,
            fixes: prf.fixes,
            assumptions: prf.assumptions,
            method: Do(m'),
            display: prf.display,
          })
        | Error(e) => ProofError({raw: prf, rule, msg: e})
        }
      | None =>
        Checked({
          rule,
          fixes: prf.fixes,
          assumptions: prf.assumptions,
          method: Goal,
          display: prf.display,
        })
      }
    | Error(e) => ProofError({raw: prf, rule, msg: e})
    }
  } // result<checked,string>

  let substituteChecked = (prf: checked, ctx: Context.t, subst: Term.subst) => {
    switch prf {
    | Checked(prf) =>
      check(ctx, Checked(prf)->uncheck->substitute(subst), prf.rule->Rule.substitute(subst))
    | ProofError({raw, rule, msg}) =>
      ProofError({raw: raw->substitute(subst), rule: rule->Rule.substitute(subst), msg})
    }
  }
}

/*
  
    
  
 */
